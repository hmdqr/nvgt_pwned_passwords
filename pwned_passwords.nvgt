/*
Pwned Passwords Checker for NVGT
Checks if a password has been exposed in data breaches using 
the Have I Been Pwned API (https://haveibeenpwned.com/Passwords)

Security: Uses k-anonymity model - only the first 5 characters of the 
SHA1 hash are sent to the API, so your actual password is NEVER transmitted.

Author: Hamad AlQassar
License: MIT
API Documentation: https://haveibeenpwned.com/API/v3#PwnedPasswords
*/

// Constants
const int PWNED_ERROR = -1;
const int PWNED_SAFE = 0;
const string PWNED_API_URL = "https://api.pwnedpasswords.com/range/";
const string PWNED_USER_AGENT = "NVGT-PwnedPasswordsChecker/1.0"; //change it. 

/*
check_pwned_password
Check if a password has been exposed in data breaches.
int check_pwned_password(const string&in password, string&out error_message);
Arguments:
  string password: the password to check (plaintext).
  string error_message: output parameter for error details if check fails.
Returns:
  int: number of times found in breaches. Returns PWNED_SAFE (0) if not found, PWNED_ERROR (-1) on error.
Remarks:
  This function uses the Have I Been Pwned range API which implements k-anonymity.
  Only the first 5 characters of the SHA1 hash are sent to the server.
  Your actual password never leaves your device.
*/
int check_pwned_password(const string&in password, string&out error_message) {
    if (password.empty()) {
        error_message = "Password cannot be empty";
        return PWNED_ERROR;
    }
    
    string sha1_hash = string_hash_sha1(password, false).upper();
    string prefix = sha1_hash.substr(0, 5);
    string suffix = sha1_hash.substr(5);
    
    // Prepare headers with Add-Padding for extra privacy
    name_value_collection headers;
    headers["Add-Padding"] = "true";
    headers["User-Agent"] = PWNED_USER_AGENT;
    
    // Make HTTP request
    http h;
    h.user_agent = PWNED_USER_AGENT;
    if (!h.get(PWNED_API_URL + prefix, headers)) {
        error_message = "Failed to send HTTP request";
        return PWNED_ERROR;
    }
    
    // Wait for response
    while (!h.complete) wait(5);
    
    if (h.status_code != HTTP_OK) {
        error_message = "HTTP Error " + h.status_code;
        return PWNED_ERROR;
    }
    
    string body = h.response_body;
    if (body.empty()) {
        error_message = "Empty response from API";
        return PWNED_ERROR;
    }
    
    // Parse response - handle both \n and \r\n line endings
    string[]@ lines = body.split("\n");
    for (uint i = 0; i < lines.length(); i++) {
        string line = lines[i].trim_whitespace();
        if (line.empty()) continue;
        
        string[]@ parts = line.split(":");
        if (parts.length() != 2) continue;
        
        // Compare hash suffix (uppercase for consistency)
        if (parts[0].trim_whitespace().upper() == suffix) {
            return parse_int(parts[1].trim_whitespace());
        }
    }
    
    return PWNED_SAFE;
}

/*
check_pwned_password (simplified)
Check if a password has been exposed in data breaches.
int check_pwned_password(const string&in password);
Arguments:
  string password: the password to check.
Returns:
  int: number of times found in breaches (0 = safe, -1 = error).
*/
int check_pwned_password(const string&in password) {
    string error;
    return check_pwned_password(password, error);
}

/*
check_pwned_password_message
Check password and return a human-readable message.
string check_pwned_password_message(const string&in password);
Arguments:
  string password: the password to check.
Returns:
  string: user-friendly result message.
Example:
  string result = check_pwned_password_message("password123");
  alert("Result", result);
*/
string check_pwned_password_message(const string&in password) {
    string error;
    int count = check_pwned_password(password, error);
    
    if (count == PWNED_ERROR) {
        return "Error checking password: " + error;
    } else if (count == PWNED_SAFE) {
        return "Good news! This password has NOT been found in any known data breaches.";
    } else {
        return "WARNING! This password has been found " + count + " times in data breaches. You should change it immediately!";
    }
}

/*
is_password_safe
Check if a password is safe (not found in breaches).
bool is_password_safe(const string&in password);
Arguments:
  string password: the password to check.
Returns:
  bool: true if safe, false if breached or error.
Example:
  if (is_password_safe("mySecurePassWd@cs!")) speak("Password is safe!");
*/
bool is_password_safe(const string&in password) {
    return check_pwned_password(password) == PWNED_SAFE;
}
